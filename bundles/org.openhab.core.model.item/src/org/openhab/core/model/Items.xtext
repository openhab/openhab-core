grammar org.eclipse.smarthome.model.Items

// disabled due to conflicts between the custom ID rule and the common INT rule
// with org.eclipse.xtext.common.Terminals

hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate items "http://www.eclipse.org/smarthome/model/Items"

ItemModel :
	{ItemModel} (items+=ModelItem)*
;

ModelItem :
	(ModelNormalItem | ModelGroupItem) name=ID
	(label=STRING)?
	('<' icon=(ID|STRING) '>')?
	('(' groups+=ID (',' groups+=ID)* ')')? 
	('[' tags+=(ID|STRING) (',' tags+=(ID|STRING))* ']')?
	('{' bindings+=ModelBinding (',' bindings+=ModelBinding)* '}')? 
;

ModelGroupItem :
	{ModelGroupItem} 'Group' (':' type=ModelItemType ( ':' function=ModelGroupFunction ('(' args+=(ID|STRING) (',' args+=(ID|STRING))* ')')?)?)?
;

enum ModelGroupFunction :
	EQUALITY='EQUALITY' | AND='AND' | OR='OR' | NAND='NAND' | NOR='NOR' | AVG='AVG' | SUM='SUM' | MAX='MAX' | MIN='MIN' | COUNT='COUNT' | LATEST='LATEST' | EARLIEST='EARLIEST'
;

ModelNormalItem :
	type=ModelItemType
;

ModelItemType :
	BaseModelItemType | ('Number' (':' ID)?)
;
BaseModelItemType :
	'Switch' | 'Rollershutter' | 'String' | 'Dimmer' | 'Contact' | 'DateTime' | 'Color' | 'Player' | 'Location' | 'Call' | 'Image'
;

ModelBinding:
	type=ID '=' configuration=STRING   
	('['
        properties+=ModelProperty? (',' properties+=ModelProperty)*
    ']')?
;

ModelProperty:
    key=ID '=' value=ValueType
;

ValueType returns ecore::EJavaObject:
    STRING | NUMBER | BOOLEAN
;

BOOLEAN returns ecore::EBoolean: 
    'true' | 'false'
;

NUMBER returns ecore::EBigDecimal:
    ID ('.' ID )?
;

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_'|'0'..'9') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;

terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
