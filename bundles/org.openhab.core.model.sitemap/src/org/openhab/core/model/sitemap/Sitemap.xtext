grammar org.openhab.core.model.sitemap.Sitemap with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate sitemap "https://openhab.org/model/Sitemap"

SitemapModel:
    'sitemap' ModelSitemap;

ModelSitemap:
    name=ID ('label=' label=STRING)? ('icon=' icon=STRING)? '{'
    (children+=ModelWidget)+
    '}';

ModelWidget:
    (ModelLinkableWidget | ModelNonLinkableWidget);

ModelNonLinkableWidget:
    ModelSwitch | ModelSelection | ModelSlider | ModelSetpoint | ModelVideo | ModelChart | ModelWebview | ModelColorpicker | ModelColortemperaturepicker | ModelMapview | ModelInput | ModelButton | ModelDefault;

ModelLinkableWidget:
    (ModelText | ModelGroup | ModelImage | ModelFrame | ModelButtongrid)
    ('{'
    (children+=ModelWidget)+
    '}')?;

ModelFrame:
    {ModelFrame} 'Frame' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelText:
    {ModelText} 'Text' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelGroup:
    'Group' (('item=' item=GroupItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelImage:
    'Image' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('url=' url=STRING) | ('refresh=' refresh=INT) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelVideo:
    'Video' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('url=' url=STRING) | ('encoding=' encoding=STRING) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelChart:
    'Chart' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('service=' service=STRING) | ('refresh=' refresh=INT) | ('period=' period=Period) |
    ('legend=' legend=BOOLEAN_OBJECT) | ('forceasitem=' forceAsItem=BOOLEAN_OBJECT) |
    ('yAxisDecimalPattern=' yAxisDecimalPattern=(STRING)) |
    ('interpolation=' interpolation=(STRING)) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelWebview:
    'Webview' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('height=' height=INT) | ('url=' url=STRING) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelSwitch:
    'Switch' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('mappings=' mappings=ModelMappingList) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelMapview:
    'Mapview' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('height=' height=INT) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelSlider:
    'Slider' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    (switchEnabled?='switchSupport') | (releaseOnly?='releaseOnly') |
    ('minValue=' minValue=Number) | ('maxValue=' maxValue=Number) | ('step=' step=Number) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelSelection:
    'Selection' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('mappings=' mappings=ModelMappingList) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelSetpoint:
    'Setpoint' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('minValue=' minValue=Number) | ('maxValue=' maxValue=Number) | ('step=' step=Number) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelColorpicker:
     'Colorpicker' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelColortemperaturepicker:
    'Colortemperaturepicker' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('minValue=' minValue=Number) | ('maxValue=' maxValue=Number) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelInput:
    'Input' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('inputHint=' inputHint=STRING) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelButtongrid:
    {ModelButtongrid} 'Buttongrid' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('buttons=' buttons=ModelButtonDefinitionList) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelButton:
    'Button' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('row=' row=INT) | ('column=' column=INT) | (stateless?='stateless') |
    ('click=' cmd=Command) | ('release=' releaseCmd=Command) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelDefault:
    'Default' (('item=' item=ItemRef) | ('label=' label=(ID | STRING)) |
    ('icon=' icon=Icon) | ('icon=' IconRules=ModelIconRuleList) | ('staticIcon=' staticIcon=Icon) |
    ('height=' height=INT) |
    ('labelcolor=' labelColor=ModelColorArrayList) |
    ('valuecolor=' valueColor=ModelColorArrayList) |
    ('iconcolor=' iconColor=ModelColorArrayList) |
    ('visibility=' visibility=ModelVisibilityRuleList))*;

ModelButtonDefinitionList returns ModelButtonDefinitionList:
    '[' elements+=ModelButtonDefinition (',' elements+=ModelButtonDefinition)* ']'
;

ModelButtonDefinition:
    row=INT ':' column=INT ':' cmd=Command '=' label=(ID | STRING) ('=' icon=Icon)?;

ModelMappingList returns ModelMappingList:
    '[' elements+=ModelMapping (',' elements+=ModelMapping)* ']'
;

ModelMapping:
    cmd=Command (':' releaseCmd=Command)? '=' label=(ID | STRING) ('=' icon=Icon)?;

ModelColorArrayList returns ModelColorArrayList:
    '[' elements+=ModelColorArray (',' elements+=ModelColorArray)* ']'
;

ModelColorArray:
    ((conditions+=ModelCondition ('AND' conditions+=ModelCondition)*) '=')? (arg=STRING);

ModelIconRuleList returns ModelIconRuleList:
    '[' elements+=ModelIconRule (',' elements+=ModelIconRule)* ']'
;

ModelIconRule:
    ((conditions+=ModelCondition ('AND' conditions+=ModelCondition)*) '=')? (arg=Icon);

ModelVisibilityRuleList returns ModelVisibilityRuleList:
    '[' elements+=ModelVisibilityRule (',' elements+=ModelVisibilityRule)* ']'
;

ModelVisibilityRule:
    conditions+=ModelCondition ('AND' conditions+=ModelCondition)*;

ModelCondition:
    (item=ID)? (condition=('==' | '>' | '<' | '>=' | '<=' | '!='))? (sign=('-' | '+'))? (state=XState);

ItemRef:
    ID;

GroupItemRef:
    ID;

Icon returns ecore::EString:
    STRING | (ID ':' (ID ':')?)? IconName;

// Allow hyphen inside an icon name
IconName:
    (ID '-')* ID;

Period:
    (ID '-')? ID;

Command returns ecore::EString:
    Number | ID | STRING;

Number returns ecore::EBigDecimal:
    '-'? (INT | FLOAT);

XState returns ecore::EString:
    INT | ID | STRING | FLOAT;

@Override 
terminal ID:
    ('^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*) |
    (('0'..'9')+ ('a'..'z' | 'A'..'Z' | '_') ('0'..'9' | 'a'..'z' | 'A'..'Z' | '_')*);

terminal FLOAT returns ecore::EBigDecimal:
    INT '.' INT;

BOOLEAN_OBJECT returns ecore::EBooleanObject:
    'true' | 'false';
